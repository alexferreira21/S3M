<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"  height="100%" width="100%" xmlns:mygoogle="mygoogle.*">
	
	<fx:Metadata>
		[Event(name="voltarEvent", type="flash.events.Event")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import com.google.maps.LatLng;
			import com.google.maps.MapMouseEvent;
			import com.google.maps.services.ClientGeocoder;
			import com.google.maps.services.GeocodingEvent;
			import com.google.maps.services.Placemark;
			
			import controller.IncluirPortalController;
			
			import entity.Equipamento;
			import entity.Municipio;
			import entity.Portal;
			import entity.UF;
			
			import main.MarkerPortalEvent;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.EventPriority;
			import mx.core.INavigatorContent;
			import mx.events.ListEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import mygoogle.MarkerPortal;
			import mygoogle.events.MapaEvent;
			
			import util.UFUtil;
			
			[Bindable]
			private var portalController: IncluirPortalController = new IncluirPortalController(); //FAZER UMA INTERFACE CONTROLLER;
			private var emEdicao: Boolean = false;
			private var markerEmEdicao: MarkerPortal;
			private var markers: ArrayCollection = new ArrayCollection();
			
			
			protected function mapaProntoHandler(event:MapaEvent):void
			{
				dgPortais.addEventListener("editarItem",editarItem);
				dgPortais.addEventListener("removerItem",removerItem);
				mapa.map.addEventListener(MapMouseEvent.CLICK,criarPortal, false, EventPriority.DEFAULT, true);
				
				portalController.carregarPortais(carregarPortaisSucesso,carregarPortaisFalha);
				
			}
			
			protected function adicionarMarkerPortal(portal: Portal):void
			{
				var markerPortal:MarkerPortal = new MarkerPortal(portal.latLng);
				markers.addItem(markerPortal);
				mapa.map.addOverlay(markerPortal);
				markerPortal.addEventListener(MarkerPortalEvent.ANTENA_SELECIONADA,selecionarMarkerPortal);
			}
			
			private function selecionarMarkerPortal(event: MarkerPortalEvent):void
			{
				var latLngSelecionada: LatLng = event.latLng;
				
				if(markerEmEdicao != null)
				{
					markerEmEdicao.isSelected = !markerEmEdicao.isSelected;
					markerEmEdicao = null;
				}
				
				for each(var portalItem: Portal in dgPortais.dataProvider)
				{
					if(portalItem.latLng.equals(latLngSelecionada))
						{
							dgPortais.selectedItem = portalItem;
						}
				}
				
				
			}
			
			private function carregarPortaisSucesso(event : ResultEvent) : void 
			{
				portalController.portais = ArrayCollection(event.result);
				
				if(portalController.portais.length > 0)
				{
					for each(var itemPortal:Portal in  portalController.portais)
					{
						adicionarMarkerPortal(itemPortal);
					}
				}
				
				cursorManager.removeBusyCursor();	
			}
			
			private function carregarPortaisFalha(event : FaultEvent) : void 
			{
				cursorManager.removeBusyCursor();
				Alert.show(event.fault.message);
			}
			
			protected function criarPortal(event:MapMouseEvent):void
			{
				
				if(!emEdicao)
				{
					var portal:Portal = new Portal();
					portal.equipamento = new ArrayCollection();
					portal.latLng = event.latLng;
					adicionarMarkerPortal(portal);
					portalController.geocodificar(event.latLng.toString(),sucessoGeocodificacao,falhaGeocodificacao);
					editarPortal(portal);
				}
				else
				{
					Alert.show("Não é possível adicionar um portal enquanto estiver no modo edição");
				}
				
			}
			
			protected function sucessoGeocodificacao(event: ResultEvent):void
			{
				var ufRetornada: UF = UF(event.result);
				portalController.portalEmEdicao.municipio.uf = ufRetornada;
			
				for each(var ufItem:UF in cbUF.dataProvider)
				{
					if(ufItem.idUF ==  ufRetornada.idUF)
					{
						cbUF.selectedItem = ufItem;
						break;
					}
				}
			}
			
			protected function falhaGeocodificacao(event :FaultEvent):void
			{
				Alert.show(event.fault.message);
			}
			
			protected function editarPortal(portal:Portal):void
			{
				emEdicao = true;
				
				portalController.portalEmEdicao = portal; 
				vsPortal.selectedChild = INavigatorContent(vsPortal.getChildByName("edicao"));	
			}
			
			
			protected function editarItem(e:Event):void
			{
				editarPortal(Portal(dgPortais.selectedItem));
			}
			
			protected function removerItem(e:Event):void
			{
				var portal:Portal = Portal(dgPortais.selectedItem);
				
				portalController.removerPortal(portal,removerPortalSucesso,removerPortalFalha);
			}
			
			private function removerPortalSucesso(event: ResultEvent):void
			{
				var portalExcluido: Portal = Portal(event.result);
				var dp: ArrayCollection = ArrayCollection(dgPortais.dataProvider);
				
				for each (var portalItem: Portal in dp)
				{
					if(portalItem.idPortal == portalExcluido.idPortal)
					{
						dp.removeItemAt(dp.getItemIndex(portalItem));
					}
						
				}
				
				for each (var markerPortalItem: MarkerPortal in markers)
				{
					if(markerPortalItem.portal.latLng.equals(portalExcluido.latLng))
					{
						mapa.map.removeOverlay(markerPortalItem);
					}
				}
			}
			
			private function removerPortalFalha(event: FaultEvent):void
			{
				
				Alert.show(event.fault.message);
			}
			
			
			protected function sairEdicao():void
			{
				
				portalController.portalEmEdicao = null;
				emEdicao = false;
				vsPortal.selectedChild = INavigatorContent(vsPortal.getChildByName("exibicao"));
			}
			
			protected function cancelarEdicao():void
			{
				if(isNaN(portalController.portalEmEdicao.idPortal)){
					for each (var markerPortalItem: MarkerPortal in markers)
					{
						if(markerPortalItem.portal.latLng.equals(portalController.portalEmEdicao.latLng))
						{
							mapa.map.removeOverlay(markerPortalItem);
						}
					}
				}
				sairEdicao();
			}
			
			
			
			protected function salvarEdicao(event:MouseEvent):void
			{
				portalController.portalEmEdicao.nome = tiNome.text;
				portalController.portalEmEdicao.altitude = parseFloat(tiAltitude.text);
				
				//if(portalController.portalEmEdicao.municipio == null) //CRIAR VARIÁVEL 'ALTEROU_MUNICIPIO' NO CHANGE DA COMBO/TEXT_INPUT DO MUNICIPIO
				//{
					var municipio:Municipio = new Municipio();
					municipio.nome = tiMunicipio.text;
					municipio.uf = cbUF.selectedItem;
					portalController.portalEmEdicao.municipio = municipio;
					
				//}
				portalController.salvarPortal();
				sairEdicao();
			}
			
			protected function adicionarEquipamento(event:MouseEvent):void
			{
				var equipamento: Equipamento = new Equipamento();
				
				equipamento.ordem = portalController.portalEmEdicao.equipamento.length+1;
				equipamento.status = 1;
				
				portalController.portalEmEdicao.equipamento.addItem(equipamento);		
			}
			
			protected function removerEquipamento(event:MouseEvent):void
			{
				Alert.show(dgEquipamentos.selectedIndex.toString());	
			}
			
			protected function selecionarItem(event:ListEvent):void
			{
				var portalSelecionado: Portal = Portal(ArrayCollection(dgPortais.dataProvider).getItemAt(event.rowIndex));
				
				if(markerEmEdicao != null)
				{
					markerEmEdicao.isSelected = !markerEmEdicao.isSelected;
				}
				
				
				for each(var markerPortalItem :MarkerPortal in markers)
				{
					if(markerPortalItem.portal.latLng.equals(portalSelecionado.latLng))
					{
						markerEmEdicao = markerPortalItem;
						markerEmEdicao.isSelected = !markerEmEdicao.isSelected;
					}
				}
			}
			
		]]>
	</fx:Script>
	
	
	<s:VGroup height="100%" width="100%">
		
		<mygoogle:Mapa id="mapa" height="100%" width="100%" mapaPronto="mapaProntoHandler(event)"/>
		
		<mx:ViewStack id="vsPortal">
			
			<s:NavigatorContent id="exibicao">
				<s:HGroup id="parteDeBaixo">
					<s:HGroup>
						<s:Form>
							<s:FormItem label="Latitude">
								<s:TextInput id="tiInputLat"/>
							</s:FormItem>
							<s:FormItem label="Longitude">
								<s:TextInput id="tiInputLng"/>
							</s:FormItem>
						</s:Form>
						<s:VGroup paddingTop="25">
							<s:Button id="btIr" label="Ir"/>
							<s:CheckBox id="cbAdcionar" label="Adicionar Portal"/>
						</s:VGroup>
					</s:HGroup>
					
					<s:Spacer width="200"/> 
					
					<mx:DataGrid id="dgPortais"  doubleClickEnabled="true" dataProvider="{portalController.portais}"
								 width="100%" itemClick="selecionarItem(event)">
						<mx:columns>
							<mx:DataGridColumn dataField="nome" headerText="Nome"/>
							<mx:DataGridColumn dataField="latitude" headerText="Latitude"/>
							<mx:DataGridColumn dataField="longitude" headerText="Longitude"/>	
							<mx:DataGridColumn headerText="Editar">
								<mx:itemRenderer>
									<fx:Component>
										<mx:HBox paddingLeft="5">
											<s:Button  label="E" width="20" click="dispatchEvent(new Event('editarItem', true))"/>
											<s:Button  label="R" width="20" click="dispatchEvent(new Event('removerItem', true))"/>
										</mx:HBox>
									</fx:Component>
								</mx:itemRenderer>
							</mx:DataGridColumn>
						</mx:columns>
					</mx:DataGrid>
					
					<s:Button id="btVoltar" label="Voltar" click="{dispatchEvent(new Event('voltarEvent'))}"/>
					
				</s:HGroup>
			</s:NavigatorContent>
			
			<s:NavigatorContent id="edicao">
				<mx:HBox>
					<s:Form>
						<s:FormItem label="Nome">
							<s:TextInput id="tiNome" text="{portalController.portalEmEdicao.nome}"/>					
						</s:FormItem>
						
						<s:FormItem label="Cidade">
							<s:TextInput id="tiMunicipio" text="{portalController.portalEmEdicao.municipio.nome}"/>
						</s:FormItem>
						
						<s:FormItem label="UF">
							<s:ComboBox id="cbUF" dataProvider="{portalController.ufDataProvider}" selectedIndex="-1"/>
						</s:FormItem>
					</s:Form>
					
					<s:Form>
						<s:FormItem label="Altitude">
							<s:TextInput id="tiAltitude" text="{portalController.portalEmEdicao.altitude}"/>
						</s:FormItem>
						
						<s:FormItem label="Latitude">
							<s:TextInput id="tiLatitude" text="{portalController.portalEmEdicao.latitude}"/>
						</s:FormItem>
						
						<s:FormItem label="Longitude">
							<s:TextInput id="tiLongitude" text="{portalController.portalEmEdicao.longitude}"/>
						</s:FormItem>
					</s:Form>
					
					<s:VGroup>
						<s:Label text="Equipamentos"/> 
						
						<mx:DataGrid id="dgEquipamentos" dataProvider="{portalController.portalEmEdicao.equipamento}" editable="true">
							<mx:columns>
								<mx:DataGridColumn dataField="nome" headerText="Nome"/>								
								<mx:DataGridColumn dataField="ordem" headerText="Ordem"/>								
								<mx:DataGridColumn dataField="status" headerText="Status"/>
							</mx:columns>
						</mx:DataGrid>
					</s:VGroup>
					
					<s:VGroup y="{dgEquipamentos.y}">
						<s:Button id="btAdicionarEquipamento" label="Adicionar Equipamento" click="adicionarEquipamento(event)"/>
						<s:Button id="btRemoverEquipamento" label="Remover Equipamento" click="removerEquipamento(event)"/>
					</s:VGroup>
					
					<s:Spacer width="100"/>
					
					<s:VGroup height="100%">
						<s:Button id="btSalvar" label="Salvar" height="50%" click="salvarEdicao(event)"/> 
						<s:Button id="btCancelar" label="Cancelar" height="50%" click="cancelarEdicao()"/>
						
					</s:VGroup>
					
				</mx:HBox>
				
			</s:NavigatorContent>
			
			
		</mx:ViewStack>
		
		
		
		
		
	</s:VGroup>
</s:Group>